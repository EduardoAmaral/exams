plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'jacoco'
	id "org.sonarqube" version "2.8"
}

group = 'com.eamaral'
version = '0.0.1-SNAPSHOT'
targetCompatibility = 11
sourceCompatibility = 11

springBoot {
	mainClassName = "com.eamaral.exams.configuration.ExamsApplication"
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

sonarqube {
	properties {
		property "sonar.sourceEncoding", "UTF-8"
		property "sonar.projectKey", "EduardoAmaral_exams"
		property "sonar.organization", "$System.env.EXAMS_SONAR_ORG"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.login", "$System.env.EXAMS_SONAR_TOKEN"
		property "sonar.coverage.exclusions", ["*Converter.java", "*DTO.java", "*Entity.java", "**.configuration"]
	}
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}

dependencies {
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'

	implementation group: 'com.google.code.gson', name: 'gson'
	implementation group: 'javax.xml.bind', name: 'jaxb-api'

	implementation 'io.springfox:springfox-swagger2:3.0.0-SNAPSHOT'
	implementation 'io.springfox:springfox-swagger-ui:3.0.0-SNAPSHOT'
	implementation 'io.springfox:springfox-data-rest:3.0.0-SNAPSHOT'
	implementation 'io.springfox:springfox-bean-validators:3.0.0-SNAPSHOT'

	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'

	implementation 'org.projectlombok:lombok'
	testImplementation 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	testImplementation 'junit:junit:4.13'
	testImplementation 'org.assertj:assertj-core'
	testImplementation 'com.github.tomakehurst:wiremock-jre8:2.26.0'
	testImplementation 'io.rest-assured:rest-assured', { exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'}
	testImplementation 'io.rest-assured:spring-mock-mvc'
	testImplementation 'org.springframework.boot:spring-boot-starter-test', { exclude group: 'org.junit.vintage', module: 'junit-vintage-engine' }
}

configurations{
	all*.exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
	all*.exclude group: "log4j", module: "log4j"
	all*.exclude group: "org.junit.jupiter", module: "junit-jupiter-api"
}

test {

	testLogging {
		events "passed", "skipped", "failed"
		exceptionFormat "full"
	}

}

task setup(type: Exec) {
	commandLine 'docker-compose', 'up', '-d --build --remove-orphans'
}

bootRun {
	jvmArgs=["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005"]
	systemProperty "spring.profiles.active", "local"
}