plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'jacoco'
	id 'org.sonarqube' version '3.0'
}

group = 'com.eamaral'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

springBoot {
	mainClassName = 'com.eamaral.worker.configuration.WorkerApplication'
}

jacocoTestReport {
	dependsOn test

	reports {
		xml.enabled true
	}
}


sonarqube {
	properties {
		property 'sonar.sourceEncoding', 'UTF-8'
		property 'sonar.projectKey', 'eduardoamaral_exams_worker'
		property 'sonar.organization', 'eduardoamaral'
		property 'sonar.host.url', 'https://sonarcloud.io'
		property 'sonar.login', "$System.env.EXAMS_SONAR_TOKEN"
		property 'sonar.coverage.exclusions', ['**/*Entity.java', '**/configuration/*']
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	jcenter()
}

dependencies {
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	implementation 'org.springframework.boot:spring-boot-starter-batch'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-solr'

	compileOnly 'org.projectlombok:lombok'

	runtimeOnly 'org.postgresql:postgresql'

	annotationProcessor 'org.projectlombok:lombok'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	testImplementation('org.apache.solr:solr-solrj:8.4.1') {
		exclude group: 'org.slf4j', module: 'slf4j-api'
		exclude group: 'commons-logging', module: 'commons-logging'
	}

	testImplementation('org.apache.solr:solr-core:8.4.1') {
		exclude group: 'org.slf4j', module: 'slf4j-api'
		exclude group: 'commons-logging', module: 'commons-logging'
	}

	testImplementation 'org.restlet.jee:org.restlet'
	testImplementation 'org.springframework.batch:spring-batch-test'
	testImplementation 'org.awaitility:awaitility:4.0.3'
	testImplementation 'com.h2database:h2'
}

configurations {
	all {
		exclude group: 'log4j', module: 'log4j'
		exclude group: 'ch.qos.logback', module: 'logback-classic'
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
}

test {
	useJUnitPlatform()
}

bootRun {
	jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5015']
	systemProperty 'spring.profiles.active', 'local'
}